apply plugin: 'com.android.application'

android {
	namespace 'ac.robinson.mediaphone'
	testNamespace namespace + '.test'
	compileSdkVersion 33

	def getVersionCode = { ->
		return 52
	}

	defaultConfig {
		manifestPlaceholders = [providerId: 'mediaphone']
		applicationId 'ac.robinson.' + manifestPlaceholders.providerId
		buildConfigField 'String', 'PROVIDER_ID', '"' + manifestPlaceholders.providerId + '"'

		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

		targetSdkVersion 33
		minSdkVersion 14
		versionCode getVersionCode()
		versionName '1.7.0'
		// versionNameSuffix = '-beta-1'
		resourceConfigurations += ['en', 'es', 'fr', 'nl', 'pt', 'pl', 'ru']
	}

	buildTypes {
		all {
			buildConfigField 'java.util.Date', 'BUILD_TIME', 'new java.util.Date(' + Calendar.getInstance()
					.getTimeInMillis() + 'L)'
			buildConfigField 'java.util.concurrent.atomic.AtomicBoolean', 'IS_TESTING', 'new java.util.concurrent.atomic' + '' +
					'.AtomicBoolean(false)'
		}

		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mediaphone.txt'

			shrinkResources true
		}

		// used solely for automatic screenshot generation - requires selecting in build variants pane to run via Android Studio
		testBuildType 'screenshots'
		screenshots {
			initWith debug
			matchingFallbacks = ['debug']
		}
	}

	// automatically regenerate fastlane metadata for new releases
	task generateMetadata(type: Exec) {
		doFirst {
			workingDir '../fastlane/'
			commandLine 'python3', 'xmltofastlane.py'
			args '--versionCode', getVersionCode()
		}
	}
	tasks.whenTaskAdded { task ->
		if (task.name == 'assembleRelease') {
			task.finalizedBy generateMetadata
		}
	}
}

dependencies {
	implementation project(':MediaUtilities')

	implementation 'com.google.android.material:material:1.8.0' // for overall UI appearance
	implementation 'androidx.exifinterface:exifinterface:1.3.6' // for auto-selection of export resolution
	implementation 'androidx.core:core:1.10.0' // for FileProvider
	implementation 'androidx.documentfile:documentfile:1.0.1' // for Storage Access Framework

	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

	// fix dependency conflicts with kotlin-stdlib - see: https://youtrack.jetbrains.com/issue/KT-54136/
	constraints {
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
			because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
		}
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
			because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
		}
	}
}
