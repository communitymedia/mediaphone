apply plugin: 'com.android.application'

android {
	compileSdkVersion 32

	defaultConfig {
		manifestPlaceholders = [providerId: 'mediaphone']
		applicationId 'ac.robinson.' + manifestPlaceholders.providerId
		buildConfigField 'String', 'PROVIDER_ID', '"' + manifestPlaceholders.providerId + '"'

		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

		targetSdkVersion 32
		minSdkVersion 14
		versionCode 51
		versionName '1.6.9'
		// versionNameSuffix = '-beta-1'
		resConfigs 'en', 'es', 'fr', 'nl', 'pt', 'pl', 'ru'
	}

	buildTypes {
		all {
			buildConfigField 'java.util.Date', 'BUILD_TIME', 'new java.util.Date(' + Calendar.getInstance()
					.getTimeInMillis() + 'L)'
			buildConfigField 'java.util.concurrent.atomic.AtomicBoolean', 'IS_TESTING', 'new java.util.concurrent.atomic' + '' +
					'.AtomicBoolean(false)'
		}

		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mediaphone.txt'

			shrinkResources true
		}

		// used solely for automatic screenshot generation - requires selecting in build variants pane to run via Android Studio
		testBuildType 'screenshots'
		screenshots {
			initWith debug
			matchingFallbacks = ['debug']
		}
	}

	// automatically regenerate fastlane metadata for new releases
	task generateMetadata(type: Exec) {
		doFirst {
			workingDir '../fastlane/'
			commandLine 'python3', 'xmltofastlane.py'
			args '--versionCode', "$project.android.defaultConfig.versionCode"
		}
	}
	tasks.whenTaskAdded { task ->
		if (task.name == 'assembleRelease') {
			task.finalizedBy generateMetadata
		}
	}
}

dependencies {
	implementation project(':MediaUtilities')

	implementation 'com.google.android.material:material:1.6.1' // for overall UI appearance
	implementation 'androidx.exifinterface:exifinterface:1.3.3' // for auto-selection of export resolution
	implementation 'androidx.core:core:1.8.0' // for FileProvider
	implementation 'androidx.documentfile:documentfile:1.0.1' // for Storage Access Framework

	androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
	androidTestImplementation 'androidx.test.ext:junit:1.1.3'
	androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
